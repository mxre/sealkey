# Maintainer: Max Resch <resch.max@gmail.com>

pkgname=sealkey
pkgver=0.1.7
pkgrel=1
pkgdesc="Create TPM sealed files using PCR calculated in advance"
arch=('x86_64')
url="https://github.com/mxre/sealkey"
license=('BSD')
depends=('json-c' 'keyutils' 'openssl' 'efivar' 'tpm-tools' 'trousers')
makedepends=('git')

source=("${pkgname}::git+${url}#tag=${pkgver}")
sha256sums=('SKIP')

backup=("etc/sealkey/paths.env"
        "etc/sealkey/linux.json")

build() {
	cd "$pkgname"
	make dist
}

package() {
	install -Dm644 "${pkgname}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	install -Dm755 "${pkgname}/sealkey" "${pkgdir}/usr/bin/sealkey"
	
	install -Dm644 "${pkgname}/archlinux/sealkey@.path" "${pkgdir}/usr/lib/systemd/system/sealkey@.path"
	install -Dm644 "${pkgname}/archlinux/sealkey@.service" "${pkgdir}/usr/lib/systemd/system/sealkey@.service"
	
	install -Dm644 "${pkgname}/archlinux/seal.json" "${pkgdir}/etc/sealkey/linux.json"
	install -Dm644 "${pkgname}/archlinux/paths.env" "${pkgdir}/etc/sealkey/paths.env"
}
